/*Antonio Wiege*/
//Value Compute Noise Variant with Custom Hermite Interpolation
#pragma kernel ND3CustomHermite
#pragma kernel ND2CustomHermite
//booleans cannot be set => using floats 0 = false everything else true
float planarize=0;
float choppy=0;
float turbulent=0;
float invert=0;//invert & turbulent to get ridge noise
float distortSpace=0;
//regular values ; pre-assigning values does not work in here, the written values are merely standard suggestions
RWStructuredBuffer<float> Output;//linear layout
int dispatchThreadGroupCountX=4;
StructuredBuffer<float3> DistortionData;//overwrite local block pos

int resultApplicationID;
float weight;

float planarizeAtY=0;
float planarizeIntensity=0;

float4 position=float4(0,0,0,0);//x,y,z,w with w e.g. bias, seed, etc
int noiseDimensionCount=4;

float4 size=1;
float4 rotation;

float octaveStartSize=2;
int octaveCount=5;
float octaveStepPow=2;
float octaveInfluenceBias=1;

float multiplyWith=1;
float addPostMultiply = 0;
float clampEndResultMin = 0;
float clampEndResultMax = 1;

//method head definitions
float4 Rotate(float4 p);

float ValueNoiseCustomHermite(float4 pos, float size);

float InterpolateTensionedHermite(float P0, float P1,float P2, float P3, float t);

float GridFloor(float value, float gridSize);
float2 GridFloor(float2 value, float gridSize);
float3 GridFloor(float3 value, float gridSize);
float4 GridFloor(float4 value, float gridSize);

float Hash2Float(uint hash);

uint Hash0(float seed);
uint Hash1(float seed);
uint Hash2(float seed);
uint Hash3(float seed);
uint Hash4(float seed);
uint Hash5(float seed);
uint Hash6(float seed);
uint Hash7(float seed);

[numthreads(8,8,1)]
void ND2CustomHermite (uint3 id : SV_DispatchThreadID)
{
    float value = 0;
    float div = 0;
    float scale = 1;
    int cid = id.x + id.y * dispatchThreadGroupCountX;
    float4 pos = (float4(id.x, id.y, 0, 0)+position+((distortSpace==0)?0:float4(DistortionData[cid].x,DistortionData[cid].y,DistortionData[cid].z,0)))*size;
        
        for (int o = octaveStartSize; o < octaveStartSize+octaveCount; o++)
        {
            scale = pow(abs(octaveStepPow), o);
            value += abs(ValueNoiseCustomHermite(pos, scale) *pow(scale,octaveInfluenceBias)- ((choppy!=0)?0.5:0))*((choppy!=0)?2:1);
            div +=pow(scale,octaveInfluenceBias);
        }

    value /= (div<1)?1:div;
    value= (turbulent!=0)?abs ((value-0.5)*2):value;
    value= (invert!=0)?1-value:value;
    value = clamp(value*multiplyWith+addPostMultiply-
        ((planarize!=0)?planarizeIntensity*(pos.y-planarizeAtY):0),
        clampEndResultMin,clampEndResultMax);

  switch(resultApplicationID){
      case 1:
      Output[cid] += value*weight;
      break;
      case 2:
      Output[cid] -= value*weight;
      break;
      case 3:
      Output[cid] *= value*weight;
      break;
      case 4:
      Output[cid] /= (value*weight==0)?1:value*weight;
      break;
      case 5:
      Output[cid] = value*(.5+weight*.5)+Output[cid]*(.5-weight*.5);
      break;
      case 6:
      Output[cid] %= value;
      break;
      case 7:
      Output[cid] = abs(value-Output[cid]);
      break;
      default:
      Output[cid] = value;
      break;
      }
}

[numthreads(4,4,4)]
void ND3CustomHermite (uint3 id : SV_DispatchThreadID)
{
     float value = 0;
    float div = 0;
    float scale = 1;
    int cid = id.x + id.y * dispatchThreadGroupCountX + id.z * dispatchThreadGroupCountX*dispatchThreadGroupCountX;
    float4 pos = (float4(id.x, id.y, id.z, 0)+position+((distortSpace==0)?0:float4(DistortionData[cid].x,DistortionData[cid].y,DistortionData[cid].z,0)))*size;
      
      for (int o = octaveStartSize; o < octaveStartSize+octaveCount; o++)
        {
            scale = pow(abs(octaveStepPow), o);
            value += abs(ValueNoiseCustomHermite(pos, scale) *pow(scale,octaveInfluenceBias)- ((choppy!=0)?0.5:0))*((choppy!=0)?2:1);
            div += pow(scale,octaveInfluenceBias);
        }

   value /= (div<1)?1:div;
   value= (turbulent!=0)?abs ((value-0.5)*2):value;
   value= (invert!=0)?1-value:value;
   value = clamp(value*multiplyWith+addPostMultiply-
        ((planarize!=0)?planarizeIntensity*(pos.y-planarizeAtY):0),
        clampEndResultMin,clampEndResultMax);

  switch(resultApplicationID){
      case 1:
      Output[cid] += value*weight;
      break;
      case 2:
      Output[cid] -= value*weight;
      break;
      case 3:
      Output[cid] *= value*weight;
      break;
      case 4:
      Output[cid] /= (value*weight==0)?1:value*weight;
      break;
      case 5:
      Output[cid] = value*(.5+weight*.5)+Output[cid]*(.5-weight*.5);
      break;
      case 6:
      Output[cid] %= value;
      break;
      case 7:
      Output[cid] = abs(value-Output[cid]);
      break;
      default:
      Output[cid] = value;
      break;
      }
}

//Rotation happens in 3D space. The rotation defines a vector in xyz plus the angle w to rotate around it (w 1 = 360 | 2PI)
  float4 Rotate(float4 p){
                //from here https://www.geeks3d.com/20141201/how-to-rotate-a-vertex-by-a-quaternion-in-glsl/
                if(length(rotation.xyz)==0)rotation.y=1;
                float ha =  rotation.w*3.14159265358979323846264338;
                float3 n = normalize(rotation.xyz);
                float4 q = float4(
                n.x * sin(ha),
                n.y * sin(ha),
                n.z * sin(ha),
                cos(ha));
     //from here https://code.google.com/archive/p/kri/wikis/Quaternions.wiki
     float3 v = float3(p.x,p.y,p.z);
     float3 o = v + 2.0 * cross(q.xyz, cross(q.xyz, v) + q.w * v);
     return float4(o.x,o.y,o.z,p.w);
    }

//*
float ValueNoiseCustomHermite(float4 pos, float size)
{
    pos = Rotate(pos);

    float4 o = GridFloor(pos,size); //original in size space
    float4 p = o * size; //stepped in world space
    float4 r = (pos - p) / size; //0-1 pos within step
    
    //Populate grid with random appearing values
   float points[256];
    for (int w = -1; w <= 2; w++) 
    {
        for (int z = -1; z <= 2; z++)
        {
            for (int y = -1; y <= 2; y++)
            {
                for (int x = -1; x <= 2; x++)
                {
                    int id = (x+1) + (y+1) * 4 + (z+1) * 16 + (w+1) * 64;

                    float4 pp = float4(p.x + x * size,p.y + y * size,p.z + z * size,p.w + w * size);
                    //rotation via no particular matrice, because 4D rotation algebra was skipped in this work.

                 //  pp= Rotate(pp);

                    points[id] = Hash2Float(
                 ((noiseDimensionCount>0)?       Hash1(pp.x):1) * 
                      ((noiseDimensionCount>1)?     Hash2(pp.y):1) *
                      ((noiseDimensionCount>2)?     Hash3(pp.z) :1)* 
                      ((noiseDimensionCount>3)?     Hash4(pp.w):1));
                }
            }
        }
    }
    
        int i = 0;
    for (i = 0; i < 64; i++)
    {
        points[i * 4]=InterpolateTensionedHermite(points[i * 4], points[i*4+1], points[i*4+2], points[i*4+3],r.x); 
    }
    for (i = 0; i < 16; i++)
    {
        points[i * 16]=InterpolateTensionedHermite(points[i * 16], points[i*16+4], points[i*16+8], points[i*16+12],r.y); 
    }
    for (i = 0; i < 4; i++)
    {
        points[i * 64]=InterpolateTensionedHermite(points[i * 64], points[i*64+16], points[i*64+32], points[i*64+48],r.z); 
    }
    return InterpolateTensionedHermite(points[0], points[64],points[128],points[192], r.w);
        }

//*/




float InterpolateTensionedHermite(float P0, float P1,float P2, float P3, float t)//modified: based off of Hermite Interpolation, http://paulbourke.net/miscellaneous/interpolation/, thanks pal.
{   //tension 1 ~ cosine , tension -1 overflow. Extreme deltas kept more accurate, small ones exaggerate'
    // Removed any bias from the function 
         float a0, a1, t_sqrt, t_cubic;
        float w0, w1, w2, w3;
        t_sqrt = t * t;
        t_cubic = t_sqrt * t;
          float tensionA =abs(P1 - P0) * 2 - 1;
          float tensionB = abs(P2 - P1) * 2 - 1;
          float tensionC = abs(P3 - P2) * 2 - 1;
        tensionA += tensionB;tensionA *= .5;
        tensionC += tensionB; tensionC *= .5;
       float tension = tensionA*clamp(0.5-t,0,1)+tensionB* (0.5-abs(t - 0.5))+ tensionC * clamp(t-0.5,0,1);
        a0 = (P1 - P0) * (1 - tension) / 2;
        a0 += (P2 - P1) * (1 - tension) / 2;
        a1 = (P2 - P1) * (1 - tension) / 2;
        a1 += (P3 - P2) * (1 - tension) / 2;
        w0 = 2 * t_cubic - 3 * t_sqrt + 1;
        w1 = t_cubic - 2 * t_sqrt + t;
        w2 = t_cubic - t_sqrt;
        w3 = -2 * t_cubic + 3 * t_sqrt;
        return (w0 * P1 + w1 * a0 + w2 * a1 + w3 * P2);
}



       //round position down to stepSize, minding values such as -0.1 go to -1 and not to 0
    float GridFloor(float value, float gridSize)
    {
        float r = (value-value%gridSize)/gridSize;
        if( value < 0) r-=1;
        return r;
    }

       float2 GridFloor(float2 value, float gridSize)
    {
        return float2(GridFloor(value.x, gridSize), GridFloor(value.y, gridSize));
    }

       float3 GridFloor(float3 value, float gridSize)
    {
        return float3(GridFloor(value.x, gridSize), GridFloor(value.y, gridSize), GridFloor(value.z, gridSize));
    }

      float4 GridFloor(float4 value, float gridSize)
    {
        return float4(GridFloor(value.x, gridSize), GridFloor(value.y, gridSize), GridFloor(value.z, gridSize),GridFloor(value.w, gridSize));
    }



    /*
Note on RNG:

Tried using the usual shader way with dot & frac -> smaller coordinate coverage and more questionable quality.
Tried combining with passed presets (range of numbers to pick from) -> more rng controll & overhead not worth over post control.
 Other custom things so far.. just no.
 Current Hash implementation based on https://burtleburtle.net/bob/hash/integer.html, https://burtleburtle.net/bob/hash/integer.html & based off of from H. Schechter & R. Bridson, goo.gl/RXiKaH
*/

float Hash2Float(uint hash)
{
    return clamp(float(hash) / 4294967295.0,-1,1); // 2^32-1
}

uint Hash0(float seed)
{
    uint s = asuint(seed - 1367.97546) - 2437962429u;
    s ^= 1331311319u;
    s *= 1954568973u;
    s ^= s >> 17;
    s *= 2987348578u;
    s ^= s >> 13;
    s *= 1862398427u;
    return s;
}

uint Hash1(float seed)
{
    uint s = asuint(seed + 3573.217801) + 3456735731u;
    s ^= s << 13;
    s *= 3698471546u;
    s ^= s >> 17;
    s *= 1876428345u;
    s ^= s << 5;
    return s;
}

uint Hash2(float seed)
{
    uint s = asuint(seed + 4823.753849) + 1863897315u;
    s = (s ^ 61) ^ (s >> 16);
    s *= 9;
    s = s ^ (s >> 4);
    s *= 668265261u;
    s = s ^ (s >> 15);
    return s;
}

uint Hash3(float seed)
{
    uint s = asuint(seed - 5886.34896) - 2887962599u;
    s ^= 2747636419u;
    s *= 2654435769u;
    s ^= s >> 16;
    s *= 2654435769u;
    s ^= s >> 16;
    s *= 2654435769u;
    return s;
}

uint Hash4(float seed)
{
    uint s = asuint(seed - 5846.34896) - 1887962593u;
    s ^= 1747636419u;
    s *= 3654435769u;
    s ^= s >> 11;
    s *= 1654435769u;
    s ^= s >> 14;
    s *= 3654435769u;
    return s;
}

    uint Hash5(float seed)
{
    uint s = asuint(seed + 7645.35896) + 2938475611u;
    s ^= s << 18;
    s *= 3698471546u;
    s ^= s >> 14;
    s *= 1876428345u;
    s ^= s << 7;
    return s;
}

uint Hash6(float seed)
{
    uint s = asuint(seed + 2387.357890) + 3847562931u;
    s = (s ^ 13) ^ (s >> 31);
    s *= 6;
    s = s ^ (s >> 3);
    s *= 886265269u;
    s = s ^ (s >> 17);
    return s;
}

uint Hash7(float seed)
{
    uint s = asuint(seed - 1999.86401) - 3928374651u;
    s ^= 1918275634u;
    s *= 3029385716u;
    s ^= s >> 17;
    s *= 1295847452u;
    s ^= s >> 17;
    s *= 1928636475u;
    return s;
}